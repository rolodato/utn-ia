set encoding utf8

# fitness histograms
####################

# Histogram settings
n = 150
min = 0
max = 300

width = (max - min) / n
hist(x, width) = width * floor(x/width) + width/2.0

# Leave some separation between bars
set boxwidth width * 0.9

# Remove top and top right borders
set border 3

# Number markers (tics) point outwards instead of inwards
set tics out

# Hide tics in axes that were removed
set xtics nomirror
set ytics nomirror

# Automatically set range from 0
set xrange [0<*:]
set terminal postscript enhanced eps color 16 lw 0.5
set xlabel 'Aptitud'
set ylabel 'Frecuencia'
set style fill solid 1.0 noborder
set style histogram rowstacked gap 0

do for [i=1:3] {
	set output 'img/fitness-'.i.'.eps'
	plot 'fitness-'.i.'.csv' using (hist($1, width)):(1.0) smooth frequency with boxes notitle
}

# average fitness evolution
###########################

set xlabel "IteraciÃ³n"
set ylabel "Aptitud media"
set grid

do for [i=1:3] {
	set output 'img/evolution-'.i.'.eps'
	plot 'evolution-'.i.'.csv' using ($0+1):1 notitle with lines
}
